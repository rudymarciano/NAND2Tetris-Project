class Target {
    field int y;
    field int current_score;
    field int return_value;
    field int speed;
    field int chance;


    constructor Target new(int speed1) {
        /* Speed of the bullet*/
        let speed = speed1;
        let return_value = 0;
        let chance = 3;
        let y = 0;
        return this;
    }

    method void draw_target(boolean setter) {
        do Screen.setColor(setter);
        do Screen.drawRectangle(504, y, 511, y + 50);
        do Screen.setColor(false);
        return;
    }

    method void move(Gun gun) {
        /*target move up and down*/
        var boolean set;
        var boolean flag;
        var boolean moving_down;
        var boolean fired;
        var int key;
        let flag = true;
        let moving_down = true;
        let fired = false;
        while (flag) {
            let key = Keyboard.keyPressed();
            if (key = 32 & ~(fired)) {
                let fired = true;
            }
            if (fired) {
                do gun.fire();
            }
            if (gun.hit() & chance > 0) {
                let current_score = calc_score();
                let chance = chance - 1;
                let return_value = return_value + current_score;
                do gun.disappear();
                do gun.blast(3805);
                do show_score();
                do Sys.wait(1000);
                let gun = Gun.new(0,0,speed);
                let fired = false;
            }
            if (chance = 0) {
                return;
            }
            if (y < 159 & (moving_down)) {
                do move_down();
                let y = y + 1;
            }
            if (y = 159) {
                let moving_down = false;
            }
            if (y > 0 & ~(moving_down)) {
                do move_up();
                let y = y - 1;
            }
            if (y = 0) {
                let moving_down = true;
            }
        }
        return;
    }

    method int abs(int a, int b) {
        if (a > b) {
            return (a-b);
        }
        return (b-a);
    }

    method int calc_score() {
        var int center;
        var int diff;
        let center = y + 48;
        let diff = abs(120, center);
        if (diff < 10) {
            return 100;
        }
        if (diff < 20) {
            return 80;
        }
        if (diff < 30) {
            return 60;
        }
        if (diff < 40) {
            return 40;
        }
        if (diff < 50) {
            return 20;
        }
        return 0;
    }

    method void move_down() {
        do Screen.setColor(true);
        do Screen.drawLine(504, 50 + y + 1, 511, 50 + y + 1);
        do Screen.setColor(false);
        do Screen.drawLine(504, y, 511, y);
        do Sys.wait(10);
        return;
    }

    method void move_up() {
        do Screen.setColor(true);
        do Screen.drawLine(504, y-1, 511, y-1);
        do Screen.setColor(false);
        do Screen.drawLine(504, 50 + y, 511, 50 + y);
        do Sys.wait(10);
        return;
    }

    method void show_score() {
        do Output.moveCursor(3,20);
        do Output.printString("CURRENT SCORE:");
        do Output.moveCursor(3,40);
        do Output.printString("   "); //prevent 100
        do Output.moveCursor(3,40);
        do Output.printInt(current_score);
        do Output.moveCursor(5,20);
        do Output.printString("ROCKETS LEFT:");
        do Output.moveCursor(5,40);
        do Output.printInt(chance);
        do Output.moveCursor(7, 20);
        do Output.printString("TOTAL SCORE:");
        do Output.moveCursor(7, 40);
        do Output.printInt(return_value);
        return;
    }

    method void show_final() {
       do Output.moveCursor(7, 20);
        do Output.printString("YOUR TOTAL SCORE IS:");
        do Output.moveCursor(7, 40);
        do Output.printInt(return_value);
        return;
    }

    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }
}